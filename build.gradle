defaultTasks 'clean', 'build', 'publishToMavenLocal'

buildscript {
    ext.kotlin_version = '1.0.1-2'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
sourceCompatibility = 1.6
targetCompatibility = 1.6
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven {
        url "http://www.baka.sk/maven2"
    }
}

dependencies {
    compileOnly 'com.google.android:android:2.2.1'
    compileOnly 'org.jetbrains:annotations:13.0'
    testCompile 'junit:junit:4.12'
    compile 'org.slf4j:slf4j-api:1.7.13'
    testCompile 'ch.qos.logback:logback-classic:1.1.3'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    // ziadne dalsie dependencies, pouziva sa vsade!
}

clean << {
    project.delete 'target', 'libs', 'obj'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'sk.baka.android'
            artifactId 'dirutils'
            version '1.7'
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

task javah(type: Exec, description: 'Generates C headers by running javah', group: 'build', dependsOn: classes) {
    commandLine 'javah', '-classpath', 'build/classes/main', '-o', 'jni/DirUtils.h', 'sk.baka.android.spi.AndroidFS'
}
