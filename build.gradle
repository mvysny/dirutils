defaultTasks 'clean', 'build', 'publishToMavenLocal'

project.group = 'sk.baka.android'
project.ext.artifactId = 'dirutils'
project.version = '1.9'

buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
sourceCompatibility = 1.6
targetCompatibility = 1.6
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url "http://www.baka.sk/maven2" }
}

dependencies {
    compileOnly 'com.google.android:android:2.2.1'
    testCompile 'junit:junit:4.12'
    compile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    // ziadne dalsie dependencies, pouziva sa vsade!
}

clean.doLast {
    project.delete 'target', 'libs', 'obj'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javah(type: Exec, description: 'Generates C headers by running javah', group: 'build', dependsOn: classes) {
    commandLine 'javah', '-classpath', 'build/classes/main', '-o', 'jni/DirUtils.h', 'sk.baka.android.spi.AndroidFS'
}

task ndkBuild(type: Exec, description: 'Runs the NDK build', group: 'build', dependsOn: javah) {
    commandLine 'ndk-build'
}

task zipNatives(type: Zip, description: 'Zips the native .so libs', group: 'build', dependsOn: ndkBuild) {
    classifier = 'native'
    from 'libs'
    into 'libs'
}

build.dependsOn zipNatives

// maven integration
apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.artifactId
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
            artifact zipNatives {
                classifier 'native'
            }
        }
    }
    repositories {
        maven {
            name 'rtsk'
            credentials {
                username 'moto'
                // remove once https://discuss.gradle.org/t/public-key-auth-for-sftp/10051 is fixed
                String pw = new File("${System.properties['user.home']}/.rtsk").text.trim()
                password pw
            }
            url 'sftp://rt.sk:22/home/moto/public_html/maven2'
        }
    }
}
